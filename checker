#!/usr/bin/env parrot-nqp

INIT {
    pir::load_bytecode("YAML/Tiny.pbc");
}

=begin sub

sub check($file_to_check, $deprecations_file) 

Scans $file_to_check for deprecations listen in $deprecations_file,
probably Parrot's C<api.yaml>. Returns a list of warnings (strings).

=end sub

sub check_file($file, $yaml) {
    my $parser := YAML::Tiny.new;
    my $api := $parser.read_string(slurp($yaml));
    my @deprs;
    my @deprecations;

    if $file ~~ / '.pir' $ / {
        for $api[0] {
            if $_<detection> && $_<detection><regex>
               && $_<detection><regex><pir> {
                   my $r := $_<detection><regex><pir>;
                @deprs.push(
                    [Regex::P6Regex::Compiler.compile($r), $r, $_<name>]
                );
            }
        }
    }

    my $fh := pir::new('FileHandle');
    $fh.open($file);
    my $line := 1;
    while $fh.readline -> $l {
        for @deprs -> $regex {
            my $r := $regex[0];
            if $l ~~ / $r / {
                @deprecations.push("$line: { $regex[2] }");
            }
        }
        $line++;
    }
    $fh.close;

    return @deprecations;
}

MAIN(pir::getinterp()[2]);
sub MAIN(@ARGS) {
    my $name := @ARGS.shift;
    if pir::elements(@ARGS) {
        for @ARGS -> $f {
            say($f);
            say(check_file($f, 'api.yaml').join("\n"));
        }
    } else {
        say("Usage:");
        print($name); say(" <file to check>");
    }
}

# vim: ft=perl6
